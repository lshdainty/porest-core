plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.porest'
version = '2.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot (provided - 사용하는 프로젝트에서 제공)
    compileOnly 'org.springframework.boot:spring-boot-starter-web:4.0.2'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation:4.0.2'
    compileOnly 'org.springframework.boot:spring-boot-starter-security:4.0.2'
    compileOnly 'org.springframework.boot:spring-boot-starter-aspectj:4.0.2'

    // Jackson (JSON 처리)
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.21.0'
    compileOnly 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.21.0'

    // Spring Data (페이지네이션 지원)
    compileOnly 'org.springframework.data:spring-data-commons:4.0.2'

    // Spring Data JPA (Auditing 지원)
    compileOnly 'org.springframework.boot:spring-boot-starter-data-jpa:4.0.2'

    // Swagger/OpenAPI
    compileOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'porest-core'
            from components.java

            pom {
                name = 'Porest Core'
                description = 'Core utilities, exceptions, and configurations for Porest project'
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/lshdainty/porest-core")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") ?: ""
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key") ?: ""
            }
        }
    }
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}
